#!groovy
// Jenkinsfile for ParksMap
podTemplate(
  label: "skopeo-pod",
  cloud: "openshift",
  inheritFrom: "maven",
  containers: [
    containerTemplate(
      name: "jnlp",
      image: "docker-registry.default.svc:5000/${GUID}-jenkins/jenkins-slave-maven-skopeo-centos7:v3.9",
      resourceRequestMemory: "1Gi",
      resourceLimitMemory: "2Gi"
    )
  ]
) {
  node('skopeo-pod') {
    echo "GUID: ${GUID}"
    echo "CLUSTER: ${CLUSTER}"

    // Define Maven Command to point to correct nexus_settings.xml.
    def mvnCmd = "mvn -s ../nexus_settings.xml"
    // Define Maven Sonarqube Command.
    def mvnSonar = "mvn sonar:sonar -s ../nexus_settings.xml"

    // Checkout Source Code
    stage('Checkout Source') {
      git 'https://github.com/paulgreenbank/openshift-app-development.git'
    }

    // Extract version and other properties from the pom.xml
    def groupId    = getGroupIdFromPom("ParksMap/pom.xml")
    def artifactId = getArtifactIdFromPom("ParksMap/pom.xml")
    def version    = getVersionFromPom("ParksMap/pom.xml")

    // Set the tag for the development image: version + build number
    def devTag  = "${version}-${BUILD_NUMBER}"
    // Set the tag for the production image: version
    def prodTag = "${version}"

    // Using Maven build the jar file
    stage('Build jar') {
      echo "Building version ${version}"
      sh "sed -i \"s/GUID/${GUID}/g\" nexus_settings.xml"
      dir('ParksMap') {
        sh "${mvnCmd} clean package spring-boot:repackage -DskipTests -Dcom.redhat.xpaas.repo.redhatga"
      }
    }

    // Using Maven run the unit tests
    stage('Unit Tests') {
      echo "Running Unit Tests"
      dir('ParksMap') {
        sh "${mvnCmd} test"
      }
    }
  
    // Using Maven call SonarQube for Code Analysis
    stage('Code Analysis') {
      echo "Running Code Analysis"
      dir('ParksMap') {
        sh "${mvnSonar} -Dsonar.host.url=http://sonarqube-${GUID}-sonarqube.apps.${CLUSTER}"
      }
    }

    // Publish the built jar file to Nexus
    stage('Publish to Nexus') {
      echo "Publish to Nexus"
      dir('ParksMap') {
        sh "${mvnCmd} deploy -DskipTests=true -DaltDeploymentRepository=nexus::default::http://nexus3-${GUID}-nexus.apps.${CLUSTER}/repository/releases"
      }
    }

    // Build the OpenShift Image in OpenShift and tag it.
    stage('Build and Tag OpenShift Image') {
      echo "Building OpenShift container image parksmap:${devTag}"
      // Start build with compiled file from Nexus
      sh "oc start-build parksmap --from-file=http://nexus3-${GUID}-nexus.apps.${CLUSTER}/repository/releases/com/openshift/evg/roadshow/parksmap/${version}/parksmap-${version}.jar --follow -n ${GUID}-parks-dev"
      // Tag Image with new tag generated via pipeline syntax generator in Jenkins
      openshiftTag alias: 'false', destStream: 'parksmap', destTag: "${devTag}", destinationNamespace: "${GUID}-parks-dev", namespace: "${GUID}-parks-dev", srcStream: 'parksmap', srcTag: 'latest', verbose: 'false'
    }

    // Blue/Green Deployment into Dev
    // -------------------------------------
    // Do not activate the new version yet.
    def destDevColour = "Green"
    def activeDevColour = "Blue"

    stage('Blue/Green Dev Deployment') {
      // Logic to identify next deployment colour (blue or green)
      activeDevColour = sh(returnStdout: true, script: "oc get service parksmap -n ${GUID}-parks-dev -o 'jsonpath={ .metadata.labels.activeDevColour }' --ignore-not-found=true").trim()
      if (activeDevColour == "Green") {
        destDevColour = "Blue"
      }

      // Deploy the built image to the Development Environment.
      echo "Deploying container image to Development Project"
      // Set Deployment image to correct version
      sh "oc set image dc/parksmap parksmap=docker-registry.default.svc:5000/${GUID}-parks-dev/parksmap:${devTag} -n ${GUID}-parks-dev"
      // Update configMap used to set build (blue/green) deployment
      sh "oc delete configmap parksmap-config -n ${GUID}-parks-dev --ignore-not-found"
      sh "oc create configmap parksmap-config -n ${GUID}-parks-dev --from-literal=APPNAME='MLB Parks (${destDevColour})'"
      // Deploy image
      openshiftDeploy depCfg: 'parksmap', namespace: "${GUID}-parks-dev", verbose: 'false', waitTime: '', waitUnit: 'sec'
      // Verify Deployment
      openshiftVerifyDeployment depCfg: 'parksmap', namespace: "${GUID}-parks-dev", verbose: 'false', replicaCount: '1', verifyReplicaCount: 'false', waitTime: '30', waitUnit: 'sec'
      // Recreate MLB back-end service
      sh "oc delete service parksmap -n ${GUID}-parks-dev --ignore-not-found=true"
      sh "oc expose dc parksmap --port 8080 --labels=type=parksmap-backend,activeDevColour=${destDevColour} -n ${GUID}-parks-dev"
      // Verify Service
      openshiftVerifyService apiURL: '', authToken: '', namespace: "${GUID}-parks-dev", svcName: 'parksmap', verbose: 'false'
    }

    // Copy Image to Nexus Docker Registry
    stage('Copy Image to Nexus Docker Registry') {
      echo "Copy image to Nexus Docker Registry"
      sh "skopeo copy --src-tls-verify=false --dest-tls-verify=false --src-creds openshift:\$(oc whoami -t) --dest-creds=admin:admin123 docker://docker-registry.default.svc:5000/${GUID}-parks-dev/parksmap:${devTag} docker://nexus-registry-${GUID}-nexus.apps.${CLUSTER}/${GUID}-parks-dev/parksmap:${devTag}"
      openshiftTag alias: 'false', destStream: 'parksmap', destTag: "${prodTag}", destinationNamespace: "${GUID}-parks-dev", namespace: "${GUID}-parks-dev", srcStream: 'parksmap', srcTag: "${devTag}", verbose: 'false'
    }

    // Blue/Green Deployment into Production
    // -------------------------------------
    // Do not activate the new version yet.
    def destApp   = "parksmap-green"
    def destColour = "Green"
    def activeApp = "parksmap-blue"
    def activeColour = "Blue"

    stage('Blue/Green Production Deployment') {
      // Logic to identify next deployment colour (blue or green)
      activeApp = sh(returnStdout: true, script: "oc get route parksmap -n ${GUID}-parks-prod -o jsonpath='{ .spec.to.name }' --ignore-not-found=true").trim()
      if (activeApp == "parksmap-green") {
        destApp = "parksmap-blue"
        destColour = "Blue"
      }
      echo "Active Application:      " + activeApp
      echo "Destination Application: " + destApp

      // Update the Image on the Production Deployment Config
      sh "oc set image dc/${destApp} ${destApp}=docker-registry.default.svc:5000/${GUID}-parks-dev/parksmap:${prodTag} -n ${GUID}-parks-prod"

      // Update the Config Map which contains the deployment colour for the MLBParks application
      sh "oc delete configmap parksmap-config -n ${GUID}-parks-prod --ignore-not-found=true"
      sh "oc create configmap parksmap-config --from-literal=APPNAME='ParksMap (${destColour})' -n ${GUID}-parks-prod"

      // Deploy the inactive application.
      openshiftDeploy depCfg: destApp, namespace: "${GUID}-parks-prod", verbose: 'false', waitTime: '', waitUnit: 'sec'
      openshiftVerifyDeployment depCfg: destApp, namespace: "${GUID}-parks-prod", replicaCount: '1', verbose: 'false', verifyReplicaCount: 'true', waitTime: '', waitUnit: 'sec'
      openshiftVerifyService namespace: "${GUID}-parks-prod", svcName: "${destApp}", verbose: 'false'
    }

    stage('Prod Integration Tests') {
      echo "Running Integration Tests"
      // Waiting for Application
      sleep 35
      // Curl status of application with /wz/health/ URL
      sh "curl http://${destApp}.${GUID}-parks-prod.svc.cluster.local:8080/ws/healthz/ | grep -q 'OK'"
    }

    stage('Switch dark release over to new Version') {
      // input "Switch Production?"
      echo "Switching Dark release application to ${destApp}."
      sh 'oc patch route parksmap-dark -n ${GUID}-parks-prod -p \'{"spec":{"to":{"name":"' + destApp + '"}}}\''
    }

    stage('Switch over to new Version') {
      // Automated release enabled
      // Uncomment to use dark release for pre-release testing
      //
      // input "Switch Production?"
      //
      echo "Switching Production application to ${destApp}."
      sh 'oc patch route parksmap -n ${GUID}-parks-prod -p \'{"spec":{"to":{"name":"' + destApp + '"}}}\''
    }
 
  }
}

// Convenience Functions to read variables from the pom.xml
// Do not change anything below this line.
def getVersionFromPom(pom) {
  def matcher = readFile(pom) =~ '<version>(.+)</version>'
  matcher ? matcher[0][1] : null
}
def getGroupIdFromPom(pom) {
  def matcher = readFile(pom) =~ '<groupId>(.+)</groupId>'
  matcher ? matcher[0][1] : null
}
def getArtifactIdFromPom(pom) {
  def matcher = readFile(pom) =~ '<artifactId>(.+)</artifactId>'
  matcher ? matcher[0][1] : null
}
